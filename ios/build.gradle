apply plugin: 'konan'

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://dl.bintray.com/jetbrains/kotlin-native-dependencies' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:$konan_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

konan.targets = ['iphone_sim', 'iphone']

repositories {
    maven { url = 'https://dl.bintray.com/e5l/http-client-common' }
}

konanArtifacts {
    def productsDir = new File('ios/Frameworks').absolutePath

    interop('libs') {
        defFile 'src/main/c_interop/libs.def'
        compilerOpts "-F${productsDir}"
        linkerOpts "-F${productsDir}"
        includeDirs new File(".").getAbsolutePath()
    }

    program('App') {
        enableMultiplatform true

        linkerOpts '-rpath', '@executable_path/Frameworks'
        linkerOpts "-F${productsDir}"

        target('iphone') {
            libraries {
                klib 'serialization_iphone'
            }
        }

        target('iphone_sim') {
            libraries {
                klib 'serialization_iphone_sim'
            }
        }

        dependencies {
            artifactApp "io.ktor.common.client:http-client-native:$http_client_version"
        }

        libraries {
            artifact 'libs'
        }
    }
}

dependencies {
    expectedBy project(':common')
}
